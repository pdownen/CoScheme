\subsection{Infinite streams}

% One of the uses of Copatterns is to define codata, and infinite streams are the clichÃ© example.
% Before starting our stream journey, let us recapitulate what codata is.
% For decades, functional programmers have had a reliable and versatile method for representing tree-shaped structures: inductive data types.
% These can model data of any size --- for example, lists of an arbitrary length --- but each instance must be \emph{finite}.
% Infinite information --- like a stream of input that can go on forever --- does not fit into an inductive type, so the programmer must use some other representation to model potentially infinite objects.
% Fortunately, the inductive data types used by functional programmers every day have a polar opposite: \emph{coinductive codata types}.
% The \emph{coinductive} descriptor signals that values of the type may contain infinite information.

For decades, functional programmers have had a reliable and versatile method for representing tree-shaped structures: inductive data types.
These can model data of any size --- for example, lists of an arbitrary length --- but each instance must be \emph{finite}.
But how does a program handle infinite amounts of information, that cannot possibly occupy a finite memory space?

One method of modeling infinite information is through laziness, as in the Haskell programming language.
For example, consider the usual infinite list of Fibonacci numbers in Haskell:
\begin{minted}{haskell}
fibs = 0 : 1 : zipWith (+) fib (tail fib)
\end{minted}
\hs|fibs| cannot be fully evaluated because it has no base case --- it would eventually expand out to \hs|0 : 1 : 1 : 2 : 3 : 5 : 8 : ...| forever --- but this is no problem in a non-strict language that only evaluates as much as needed.
But what if we are working within a strict language without laziness built in?
Must we give up on the approach entirely, or is there an alternate solution that works just as well with eager and lazy evaluation?

In contrast, \emph{codata} describes types defined by primitive \emph{destructors} that \emph{use} values of the codata type --- as opposed to the primitive constructors that define how to build values of a data type --- and lets us easily model infinite data in eager languages, too.
For example, the usual \agda|Stream a| codata type of infinite \agda|a|'s is defined by two destructors: \agda|Head : Stream a -> a| extracts the first element and \agda|Tail : Stream a -> Stream a| discards the first element and returns the rest.
To define new streams, we can describe how they react to different combinations of \agda|Head| and \agda|Tail| destructions using \emph{copatterns}~\cite{APTS2013C}.
Borrowing Agda's syntax, a possible copattern-based definition of the same \agda|fibs| function above is:
\begin{minted}{agda}
fibs : Stream Nat
Head fibs = 0
Head (Tail fibs) = 1
Tail (Tail fibs) = zipWith _+_ fibs (Tail fibs)
\end{minted}
However, at the moment, Agda currently does not understand if \agda|fibs| is well-founded --- it is --- and so \agda|fibs| is rejected.
As a proof assitant, Agda has has demanding requirements on all definitions to ensure well-foundedness:
they must never have unproductive infinite loops, and they must cover every possible case (when matching on arguments or copatterns, as in \agda|fibs|).
But for a general-purpose programming language, we expect to be able to write arbitrary loops that may or may not terminate.
Copattern-based definitions need to gracefully handle cases that fail to return --- either due to an infinite loop or an exception, like an unhandled case, which are semantically similar \cite{ImpreciseExceptions} --- and should generate code based on whatever is given.
In this kind of setting, the language does not enforce --- and indeed, our implementation does not check --- coverage, which is instead up to the programmer to determine.

Let us now consider some examples of programming by equational reasoning to get familiar with copatterns and how we can use them in Scheme.
% Deletion candidate
% In these examples, it can help to think about types informally to keep ourselves oriented.
For example, even in a dynamically-typed language like Scheme, linked lists can be thought of as an inductively-defined type combining two constructed forms: \scm#List a = null | (cons a (List a))#.
Likewise, infinite streams can be understood as the type of a procedure that exhibits two different behaviors at the same time: \scm#Stream a = 'head -> a & 'tail -> Stream a#.
In other words, any \scm|Stream a| is a procedure that takes one argument, and its response depends on the exact value: given \scm|'head| an \scm|a| is returned, and given \scm|'tail| another \scm|Stream a| is returned.

In order to define new coinductive processes, one of the main entry points is the top-level, multi-line \scm{define*} macro.
This macro enables us to declare codata objects through a list of equations between a copattern on the left-hand side and an expression on the right-hand side.
At the root of every copattern is a name for the object \emph{itself}, which can be inside any number of applications --- the applications may just list parameter names or more specific patterns, narrowing down the concrete arguments that match.
Using \scm{define*}, we can define the trivial \scm|zeroes| stream  --- whose \scm|'head| is \scm|0| and whose \scm|'tail| is more \scm|zeroes| --- as:
\begin{minted}{scheme}
;; zeroes : Stream nat
(define* [(zeroes 'head) = 0]
         [(zeroes 'tail) = zeroes])
\end{minted}
Streams like \scm|zeroes| are black boxes that can only be observed by passing \scm|'head| or \scm|'tail| as arguments to get their response.
Still, this is enough for many useful operations, like taking the first \scm|n| elements, which can be \scm|define*|d as:
\begin{minted}{scheme}
;; takes : (Stream a, nat) -> List a
(define* [(takes s 0) = '()]
         [(takes s n) = (cons (s 'head) (takes (s 'tail) (- n 1)))])
\end{minted}
% So that \scm|(takes zeroes 5) = '(0 0 0 0 0)|.
A constant stream is not particularly useful; more interesting streams will change over time.
For example, imagine a ``stuttering'' stream ($0, 0, 1, 1, 2, 2, 3, 3, \dots$) that repeats numbers twice before moving on.
This stream can be defined by copattern matching equations:
\begin{minted}{scheme}
;; stutter : nat -> Stream nat
(define* [ ((stutter n) 'head)        = n]
         [(((stutter n) 'tail) 'head) = n]
         [(((stutter n) 'tail) 'tail) = (stutter (+ n 1))])
\end{minted}
So that \scm|(takes (stutter 1) 10) = '(1 1 2 2 3 3 4 4 5 5)|,% 
\footnote{
  Try it!
  \url{https://github.com/pdownen/CoScheme} has implementations of \scm|define*| and related macros.
  All examples shown here are executable Scheme and Racket code.
}
because the first and second elements --- \scm|((stutter n) 'head)| and \scm|(((stutter n) 'tail) 'head)| respectively --- return the same \scm|n| before incrementing.

But why is \scm|stutter| well-defined, and how can we understand its meaning?
As in many functional languages, the \scm|=| in code really implies equality between the two sides, and this equality still holds when we plug in real values for placeholder variables like \scm|n|.
So to determine the first \scm|'head| element, of \scm|(stutter 1)|, we match the left-hand side and replace it with the right to get \scm|((stutter 1) 'head) = 1|.
Similarly, the second element is \scm|(((stutter 1) 'tail) 'head) = 1| as well.
The third element is accessed by two \scm|'tail| projections and then a \scm|'head| as the nested applications \scm|((((stutter 1) 'tail) 'tail) 'head)|, which doesn't exactly match any left-hand side.
However, equality holds in any context, and the inner application \scm|(((stutter 1) 'tail) 'tail)| \emph{does} match the third equation.
Thus, we can apply a few steps of equational reasoning to derive the expected answer \scm|2|:
\begin{minted}{scheme}
((((stutter 1) 'tail) 'tail) 'head) = ((stutter (+ 1 1)) 'tail)  ; line 3
                                    = ((stutter 2) 'head)        ; +
                                    = 2                          ; line 1
\end{minted}
% \begin{minted}{scheme}
%    ((((stutter 1) 'tail) 'tail) 'head) = ((stutter (+ 1 1)) 'tail)
%   = ((stutter 2) 'head)                =  2                                      
%   \end{minted}
So these three examples work, but is every case really covered?
The \scm|Stream Nat| interface that \scm|stutter|'s output follows allows for any number of \scm|'tail| projections followed by a final application to \scm|'head| that returns a natural number.
\scm|stutter| works its way through these projections in groups of two, eliminating a pair of \scm|'tail| projections at a time until it gets to the end case, which is either a \scm|'head| (if the total number of \scm|'tail|s is even) or a \scm|'tail| followed by \scm|'head| (if the total number of \scm|'tail|s is odd).
So \scm|stutter| behavior is defined no matter what is asked of it.
Even with other observations like \scm|takes|, which passes around partial applications of \scm|stutter| as a first-class value, internally \scm|stutter| only ``sees'' the \scm|'head| and \scm|'tail| applications from \scm|takes|, and is dormant otherwise.

Reasoning about the coverage of our copatterns is important since our implementation does not provide coverage analysis.
If we encounter an uncovered case, our implementation emits a runtime error, explaining that this is an uncovered copattern.
Non-total configurations, akin to partial functions, are not always undesirable. They can simplify the development during a prototyping phase, and if the missing case does not make sense, they can be the most semantically meaningfull.

% This framework is not limited by matching a single value in the initial group.
% To illustrate, we can define a stream that intercalates elements from two different streams using a similar configuration, but observing two arguments instead of one.

% \adriano{Maybe we can put those examples side by side in a single figure if we decrease the font size}

% \begin{minted}{scheme}
% ;; zigzag : (Stream a, Stream a) -> Stream a
% (define*
%   [ ((zigzag xs ys) 'head)        = (xs 'head)]
%   [(((zigzag xs ys) 'tail) 'head) = (ys 'head)]
%   [(((zigzag xs ys) 'tail) 'tail) = (zigzag (xs 'tail) (ys 'tail))])
% \end{minted}

With this practice under our belt, we can now directly translate canonical Fibonacci example from Agda to Scheme like so:
\begin{minted}{scheme}
;; zips-with : ((a, b) -> c, Stream a, Stream b) -> Stream c
(define*
  [((zips-with f xs ys) 'head) = (f (xs 'head) (ys 'head))]
  [((zips-with f xs ys) 'tail) = (zips-with f (xs 'tail) (ys 'tail))])

;; fibs : Stream nat
(define*
  [ (fibs 'head)        = 0]
  [((fibs 'tail) 'head) = 1]
  [((fibs 'tail) 'tail) = (zips-with + fibs (fibs 'tail))])
\end{minted}
so that \scm|(takes fibs 10)| is \scm|'(0 1 1 2 3 5 8 13 21 34)|.

\subsection{Self-referrential objects}

Codata can also be used to implement an abstract interface over regular finite data.
As an alternate syntax for \scm|define*|, we can explicitly give a top-level name to bind the definition to for external use, and on each equation give a hidden internal for self-reference and recursion.
To illustrate this, consider the following queue example, which internally refers to itself by the name \scm|self| for an object-oriented feel:
\begin{minted}{scheme}
(define* queue
  [ (self 'empty)          = (self '() '())]
  [((self in  out) 'enq x) = (self in (cons x out))]
  [((self '() '()) 'deq)   = (error "Invalid dequeue: empty queue")]
  [((self '() out) 'deq)   = ((self (reverse out) '()) 'deq)]
  [((self in  out) 'deq)   = (cons (car in) (self (cdr in) out))])
\end{minted}
This reflects the purely functional queue implementation in using two lists (an inbox and an outbox) as internal states.
We externally bound this declaration to the name \scm{queue}, but internal recursion is through the name \scm|self|.
This \scm|queue| object responds to three methods: \scm|'empty| returns a (potentially new) empty queue, \scm|'enq x| puts the \scm|x| to the end of the queue (\ie the top of the inbox), and \scm|'deq| returns the oldest enqueued element (from the top of the outbox or bottom of the inbox, as appropriate).
Thus, \scm|((((queue 'empty) 'enq 1) 'enq 2) 'deq)| returns the oldest element \scm|1| and an opaque queue object containing only \scm|2|.

Visualizing what we are defining through the lens of the object-oriented paradigm can be insightful.
In this metaphor, we can pretend that our definitions are objects and that the copatterns specify how an object should respond to a sequence of messages.
For instance, \scm{zeroes} would be an object that knows how to respond to two messages:  \scm{'head} and \scm{'tail}.

Previous works, such as \cite{Brown2009FunctionI}, show us that the concept of inheritance can be applied in the functional world.
Thus, reversing the direction of our metaphor, what should be inheritance in our abstraction? 
The answer should be familiar to functional programmers: inheritance is composition!
Our implementation of copattern matching in Scheme includes new facilities for composing code snippets compared to current functional (or object-oriented) languages.
However, to avoid unwanted surprises, the programmer does have to ask for them.
This is a small request, and can be done by replacing \scm|define*| with \scm|define-object|, as in the following file system example:
\begin{minted}{scheme}
(define-object size-file
  [(size `(file ,s)) = s])

(define-object size-dir
  [(size `(dir . ,cts)) = (apply + 8 (map size cts))])

(define size-fs (size-file 'compose size-dir))
\end{minted}
This example emulates calculating the size of files and directories in a file system.
A file has its size \scm|s| already given as metadata, and a directory contains a list of \scm|files| as its contents.
% In this definition, we used our second way of creating top-level declarations: \scm{define-object}.
% For now, it is enough to think that \scm{define-object} exposes a different abstraction, \emph{templates}, which allows us to perform our composition operations.
% Besides asking for the \scm|'compose| functionality provided by \scm|define-object|, the other new construct is \scm|try-if|, which resembles a boolean match guard and forces the copattern only to match when the predicate \scm|(null? files)| succeeds.

This is the first example illustrating our guiding principle of composition.
We declare how we should calculate the size of each type of content separately, and we glue them together afterward.
We refer to this style of composition as \emph{vertical composition}, because the result acts as if we have glued the equations together vertically as:
\begin{minted}{scheme}
(define-object size-fs
  [(size 'file s)       = s]
  [(size `(dir . ,cts)) = (apply + 8 (map size cts))]))
\end{minted}

In this definition, each appearance of \scm{size} plays a key role.
It may seem that we are \scm{size} function, but in reality, nothing is bound to \scm{size}.
Here, what is actually happening is an example of open recursion.
Each part can perform a recursive call that will respect all the parts after joining all parts together.
However, one may question: what is \scm{size}?
\scm{size} is the internal name for a recursive call.
It is not required to be the same in each component since it will only be used in the scope of that copattern.
We could have other names instead of \scm{size}, like \scm{phony}, \scm{size-dir}, \scm{self}, and \scm{this}.
The last two are familiar faces from OO languages, but nevertheless, all would be valid for our internal name.
It is also possible to omit the name in a \scm{define*} declaration.
If we remove \scm{size-file}, the internal name, the root of the copattern, will be used and exposed. 

With those out of the way, we can focus on \scm{'compose}. Here, using our OO metaphor, \scm{'compose} is a message that every object knows how to respond to. As mentioned, we are performing a \emph{vertical composition}, which you may visualize as vertically stacking the cases.
Drinking from the OO source, we also provide another variation of composition in the form of multiple inheritance/subtyping.
\begin{minted}{scheme}
(define-object (size-fs-sl <: (inherit-from-object size-fs))
  [(size `(sl ,_)) = 32])
\end{minted}
Here, we created \scm{size-fs-sl}, an extended version capable of handling symbolic links.
We use the \scm{<:} symbol to mark which ``\emph{classes}'' we should inherit from.
Thus, the result will be able to manifest all the \scm{size-fs} behavior in addition to what is defined in \scm{size-fs-sl} locally, which has greater precedence.

  
\subsection{Decomposing the expression problem}

However, definitions by copatterns are useful for more programming tasks than just streams and other infinite objects.
% In particular, we can define a depth-first search on a finite binary tree.
% For this goal, we need to specify what should happen in contexts containing leaves and nodes of a tree.
% We can create observations that match a specific shape of the input.
% Therefore, when we see an evaluation context with a leaf --- \scm{((search ('leaf e)) 'dfs)} ---, we return a singleton, and when we see a node --- \scm{((search ('node l e r)) 'dfs)} ---, we recurse on both children and append their results.
% \adriano{Do I need to talk about how DFS works?}
% \begin{minted}{scheme}
%   (define* [((search ('leaf e)) 'dfs)     = (list e)]
%            [((search ('node l e r)) 'dfs) = (append ((search l) 'dfs)
%                                                     (list e)
%                                                     ((search r) 'dfs))])
% \end{minted}
For example, consider the usual definition of a simple arithmetic expression evaluator in typed functional languages like Haskell and OCaml (we use Haskell syntax here):
\begin{minted}{haskell}
data Expr = Num Int | Add Expr Expr

eval :: Expr -> Int
eval (Num n)   = n
eval (Add l r) = eval l + eval r
\end{minted}
While Scheme does not have algebraic data types, we can encode them as a list starting with the constructor name as a quoted symbol and the arguments as the remainder of the list.
So \hs|Num 5| would be represented as the quoted list \scm|'(num 5)|, and \scm|Add l r| would be represented as the \emph{quasiquote} \scm|`(add ,l ,r)| which plugs in the values bound to variables \scm|l| and \scm|r| as the second and third elements of the list (denoted by the ``unquote'' comma \scm|,| before the variable names).
We can then use the facilities of \scm|define*| to write almost identical code in Scheme like so:
\begin{minted}{scheme}
(define* [(eval `(num ,n))    = n]
         [(eval `(add ,l ,r)) = (+ (eval l) (eval r))])
\end{minted}
Fantastic, it works!
Both the Scheme and Haskell code have the same structure.
And on the surface, they both share the same strengths and weaknesses.
From the lens of the \emph{expression problem}~\cite{ExpressionProblem}, it is easy to add new operations to existing expressions --- such as listing the numeric literals in an expression
\begin{minted}{scheme}
(define* [(list-nums `(num ,n))    = (list n)]
         [(list-nums `(add ,l ,r)) = (append (list-nums l) (list-nums r))])
\end{minted}
--- but adding new classes of expressions is hard.
For example, if we wanted to support multiplication, we could add a \hs|Mult| constructor to the \hs|Expr| data type, but this would require modifying \emph{all} existing operations and case-splitting expressions over \hs|Expr| values.
Even worse, if we wanted to support both expression languages --- with or without multiplication --- we would have to copy the code and maintain both versions.

Thankfully, our implementation of copattern matching in Scheme includes new facilities for composing code snippets compared to current functional (or object-oriented) languages.
However, to avoid unwanted surprises, the programmer does have to ask for them.
This is a small request, and can be done by replacing \scm|define*| with \scm|define-object|, such as:
\begin{minted}{scheme}
(define-object
  [(list-nums* `(num ,n))    = (list n)]
  [(list-nums* `(add ,l ,r)) = (append (list-nums* l) (list-nums* r))])
\end{minted}
The \scm|list-nums*| object behaves exactly like \scm|list-nums| in all the same contexts it works in, but in addition, it implicitly inherits additional functionality for composition defined elsewhere.
This new composition lets us break existing multi-line definitions into individual parts, and recompose them later.
For example, the evaluator can be composed in terms of separate objects for each line like so:
\begin{minted}{scheme}
(define-object [(eval-num `(num ,n)) = n])
(define-object [(eval-add `(add ,l ,r)) = (+ (eval-add l) (eval-add r))])
(define eval* (eval-num 'compose eval-add))
\end{minted}
So \scm|(eval expr)| is the same as \scm|(eval* expr)| for any well-formed expression argument.
Why program in this way?
Now, if we want to extend the functionality of existing operations --- like evaluation and listing literals --- to support a new class of expression, we can define the new special cases separately as a patch and then \emph{compose} them with the existing code as-is like so:
\begin{minted}{scheme}
(define-object [(eval-mul `(mul ,l ,r)) = (* (eval-mul l) (eval-mul r))])
(define-object [(list-mul `(mul ,l ,r)) = (append (list-mul l) (list-mul r))])

(define eval-arith (eval* 'compose eval-mul))
(define list-nums-arith (list-nums* 'compose list-mul))
\end{minted}
So for an expression \scm|(define expr1 '(add (mul (num 2) (num 3)) (num 4)))|, the extended code successfully yields the correct answers \scm|(eval-arith expr1) = 10| and \scm|(list-nums-arith expr1) = '(2 3 4)| whereas the original code fails at the \scm|'mul| case.%
\footnote{
  The astute reader might notice that for this to work, the recursive calls to \scm|eval-mul| cannot be specifically tied to this definition because it only says what to do with multiplication and fails to handle the other cases.
  Instead, recursive calls to \scm|eval-mul| must \emph{also} open to invoking the other code associated with \scm|eval-num| and \scm|eval-add| even though it not known to be associated with them yet.}
%
Note that this composition automatically generates \emph{new} functions and leaves the original code intact, which can still be used for the smaller expression language with only numbers and addition.

This example emphasizes our guiding principle: \emph{composition}.
We call combinations like \scm|(eval-num 'compose eval-add eval-mul)| \emph{vertical composition} since they behave as if we simply stacked their internal cases vertically, like in the original definition of \scm|eval|.

Not all types of language extensions are this simple, though.
Consider what happens if we want to support algebraic expressions which might have variables in them.
To evaluate a variable, we need a given environment --- mapping names to numbers --- which we can use to look up the variable's value.
\begin{minted}{scheme}
(define-object [(eval-var env `(var ,x)) = (lookup env x)])
\end{minted}
However, it is wrong to just vertically compose this variable evaluator with the previous code because the arithmetic evaluator only takes a single expression as an argument, whereas the variable evaluator needs \emph{both} an environment and an expression.
The manual way to perform this extension is routine for functional programmers: in addition to adding a new case, we have to add an extra parameter to each case, which gets passed along on all recursive calls.
% On an individual equation, this transformation looks like:
% \begin{minted}{scheme}
% [(eval     some-expr-pattern) (... (eval     sub-expr) ... )]
% ==>
% [(eval env some-expr-pattern) (... (eval env sub-expr) ...)]
% \end{minted}

It would be highly disappointing to have to rewrite our existing code in-place to do this extension.
Fortunately, our copattern language allows for another type of composition --- \emph{horizontal composition} --- which allows us to combine sequences of steps, one after another, and automatically fall through to the next case if something fails.
For this example, we can define a general procedure \scm|with-environment| to perform the above transformation, taking any extensible evaluator object expecting just an expression and threading an environment along each recursive call.
This lets us patch our existing arithmetic evaluator with an environment and then compose it with variable evaluation like so:
\begin{minted}{scheme}
(define (with-environment eval-ext)
  (object [(self env expr)
           (with-self (override-lambda* self
                        [(_ sub-expr) = (self env sub-expr)])
             (try-apply-forget eval-ext expr))]))

(define eval-alg ((with-environment (eval-arith 'unplug)) 'compose eval-var))
\end{minted}
% TODO: Maybe resume this part
The \scm|with-environment| function is the most complex code we have seen so far, but it just spells out the usual steps a functional programmer uses to modify existing code with an environment.
\begin{itemize}
\item Given the evaluator \scm|eval-ext|, it returns a new first-class \scm|object| (which is the same as \scm|define-object| without assigning a name) that expects both an environment and expression to process.
\item This new object then invokes \scm|eval-ext| by passing just the expression, except that if \scm|eval-ext| ever tries to recur with a sub-expression, the calls \scm|(self sub-expr)| gets replaced with \scm|(self env sub-expr)| just like the template transformation.
\item This transformation of the evaluator's notion of self is done by the \scm|with-self| operation, which can override the original recursive \scm|self|.
\item Finally, if none of the clauses of \scm|eval-ext| succeed, then this updated evaluator also falls through as before, forgetting the application had ever happened via \scm|try-apply-forget|.
\end{itemize}
The complete algebraic evaluator can then be made from an open-ended, extensible version of the arithmetic evaluator --- retrieved from \scm|(eval-arith 'unplug)| --- horizontally composed to take an environment and vertically composed with the single-line \scm|eval-var|.
It can now successfully evaluate algebraic expressions, such as \scm|(define expr2 '(add (var x) (mul (num 3) (var y))))|, so that running \scm|(eval-arith '((x . 10) (y . 20)) expr2)| returns \scm|70| because the environment maps \scm|x| to \scm|10| and \scm|y| to \scm|20|.

Another possible way to evaluate expressions with variables is \emph{constant folding}, a common optimization where operations are simplified unless they are blocked by variables whose values are unknown.
In other words, the evaluator might return a blocked expression if it cannot fully calculate the final number.
Ideally, we would like to extend our existing evaluator as-is, with the additional cases when blocked expressions are encountered.  
However, as written, the equation handling \scm|(eval `(add ,l ,r))| already commits to a real numeric addition, even if evaluating \scm|l| or \scm|r| does not give a numeric result.

To avoid over-committing before we know whether evaluation will successfully calculate a final number or not, we can --- at the first glance --- rewrite the basic clauses of evaluation in a more defensive style.
Essentially, this splits evaluation into two separate steps:
\begin{enumerate*}[(1)]
\item check which operation we are supposed to do and evaluate the two sub-expressions,
\item combine the two expressions according to that operation.
\end{enumerate*}
For example, the two steps for addition look like:
\begin{minted}{scheme} 
(define-object eval-add-safe
  [(self 'eval ('add l r))
  = (self 'add (self 'eval l) (self 'eval r))]
  [(self 'add x y) (try-if (and (number? x) (number? y)))
  = (+ x y)])
\end{minted}
Here, the evaluation step is explicated by a \scm|'eval| tag, to help distinguish from the other operation \scm|'add| for adding the left and right results.
Note that in this code, the \scm|'add| clause only performs a numeric addition \scm|+| if it knows for sure that \emph{both} of the arguments are actually numbers.
This is checked by the \scm|try-if| guards.
We can now compose the original base-case for evaluating numbers with this ``safer'' version of addition that fails to match cases where sub-expressions don't evaluate to numbers (multiplication could be added as well in a similar style):
\begin{minted}{scheme}
(define eval-arith-safe (eval-num 'compose eval-add-safe))
\end{minted}
So \scm|(eval-arith-safe expr1)| still evaluates to \scm|70|, but \scm|(eval-arith-safe expr2)| fails when it finds a variable sub-expression.

If it finds a variable, constant folding will just leave it alone and return an unevaluated expression rather than a final number.
Because the \scm|'eval| operation might return a (partially) unevaluated expression, we now need to handle cases where the left or right (or both) sub-expressions do not evaluate to numbers.
In each of those cases, we must reform the addition expression out of what we find, converting numbers \scm|n| into a syntax tree of the form \scm|`(num ,n)|.
\begin{minted}{scheme}
(define-object [(leave-variables 'eval ('var x)) = (list 'var x)])

(define-object reform-addition
  [(reform 'add l r) (try-if (number? l)) = (reform 'add `(num ,l) r)]
  [(reform 'add l r) (try-if (number? r)) = (reform 'add l `(num ,r))]
  [(reform 'add l r)                      = (list 'add l r)])
\end{minted}
The final constant-folding algorithm can be composed from this ``safe'' version of evaluation, along with the cases for leaving variables alone and reforming partially-evaluated additions and multiplications, which can be defined using the same approach employed in \scm|reform-addition|.
\begin{minted}{scheme}
(define constant-fold
  (eval-arith-safe 'compose leave-variables reform-addition reform-mult))
\end{minted}
So now \scm|(constant-fold 'eval expr2)| successfully returns \scm|expr2| itself (because there are no operations to perform without knowing the values of variables \scm|x| and \scm|y|).
And running \scm|(constant-fold 'eval expr3)| on the expression
\begin{minted}{scheme}
(define expr3 '(add (add (num 1) (num 1))
                    (mul (var x)
                         (mul (num 2) (add (num 2) (num 3))))))
\end{minted}
simplifies it down to \scm|'(add (num 2) (mul (var x) (num 10)))|.
To add other operations, like multiplication, we can easily define similar \scm|eval-mul-safe| and \scm|reform-multiplication|, and \scm|'compose| them with \scm|constant-fold| without having to rewrite any code.
All examples shown here are in the supplemental materials.

%%% Local Variables:
%%% mode: LaTeX
%%% TeX-master: "coscheme"
%%% End:
